
subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = JavaVersion.VERSION_1_10
    targetCompatibility = JavaVersion.VERSION_1_10

    //TODO: if hasProperty release -> version = else version= SNAPSHOT 

    //Last release version
    version = '0.85.1'
    group = 'net.sf.JRecord'
    
    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.25'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

        compileOnly 'org.projectlombok:lombok:1.18.2'
        annotationProcessor("org.projectlombok:lombok:1.18.2")
    }
    
    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        classifier = 'sources'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
            }
        }
        repositories {
            maven {
                url = "$rootProject.projectDir/repos"
            }
        }
    }

}

task assemble(type: Copy) {
    dependsOn(':cb2xml:assemble',':JRecord:assemble',':JRecord_Cbl2Json:assemble',':JRecord_Cbl2Xml:assemble')
    from "${projectDir}/cb2xml/build/libs/cb2xml.jar",
            "${projectDir}/JRecord/build/libs/JRecord.jar",
            "${projectDir}/JRecord_Cbl2Json/build/libs/JRecord_Cbl2Json.jar",
            "${projectDir}/JRecord_Cbl2Xml/build/libs/JRecord_Cbl2Xml.jar"
    into "${projectDir}/artifacts"
}

task clean(type: Delete) {
    delete "${projectDir}/artifacts"
}